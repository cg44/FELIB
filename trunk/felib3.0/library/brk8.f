C***********************************************************************
      SUBROUTINE BRK8(FUN, IFUN, DER, IDER, JDER, XI, ETA, ZETA,
     *     ITEST)
C-----------------------------------------------------------------------
C PURPOSE
C      RETURNS THE VALUES OF SHAPE FUNCTIONS AND DERIVATIVES
C      AT A SPECIFIED POINT FOR AN 8-NODED BRICK ELEMENT.  THE
C      FUNCTION IS CONTINUOUS ACROSS ELEMENT BOUNDARIES.
C
C HISTORY
C
C      COPYRIGHT (C) 1997 : CLRC, RUTHERFORD APPLETON LABORATORY
C                           CHILTON, DIDCOT, OXFORDSHIRE OX11 0QX
C
C      RELEASE 1.1  29 OCT 1979 (CG)
C      COMMENTED    10 OCT 1980 (KR)
C
C ARGUMENTS IN
C      IFUN    DIMENSION OF VECTOR FUN (.GE.8)
C      IDER    FIRST DIMENSION OF ARRAY DER (.GE.3)
C      JDER    SECOND DIMENSION OF ARRAY DER (.GE.8)
C      XI      VALUE OF LOCAL COORDINATE AT WHICH FUNCTION AND
C              DERIVATIVE VALUES REQUIRED
C      ETA     VALUE OF SECOND LOCAL COORDINATE
C      ZETA    VALUE OF THIRD LOCAL COORDINATE
C      ITEST   ERROR CHECKING OPTION
C
C ARGUMENTS OUT
C      FUN     REAL VECTOR OF DIMENSION IFUN.  FUN(I) CONTAINS
C              VALUE OF I'TH SHAPE FUNCTION AT (XI,ETA,ZETA)
C      DER     REAL ARRAY OF DIMENSIONS (IDER,JDER).  DER(I,J)
C              CONTAINS THE DERIVATIVE OF THE J'TH SHAPE
C              FUNCTION WITH RESPECT TO THE I'TH COORDINATE AT
C              THE POINT (XI,ETA,ZETA)
C
C ROUTINES CALLED
C      ERRMES
C
C     SUBROUTINE BRK8(FUN, IFUN, DER, IDER, JDER, XI, ETA, ZETA,
C    *     ITEST)
C***********************************************************************
C
      INTEGER ERRMES, IDER, IERROR, IFUN, ITEST, JDER
      DOUBLE PRECISION DER, DUMMY, ETA, ETAM, ETAP, FUN, HALF, SRNAME,
     *     VAL, VEPS, XI, XIM, XIP, ZETA, ZETAM, ZETAP
      DIMENSION DER(IDER,JDER), FUN(IFUN)
      DATA SRNAME /8H BRK8   /, HALF /0.5D0/
C
C     PARAMETER CHECKING
C
      IF (ITEST.EQ.-1) GO TO 1010
      IERROR = 0
      IF (IFUN.LT.8) IERROR = 1
      IF (IDER.LT.3 .OR. JDER.LT.8) IERROR = 2
      VAL = 1.0D0 + VEPS(DUMMY)
      IF (DABS(XI).GT.VAL .OR. DABS(ETA).GT.VAL .OR.
     *     DABS(ZETA).GT.VAL) IERROR = 3
      ITEST = ERRMES(ITEST,IERROR,SRNAME)
      IF (ITEST.NE.0) RETURN
C
C     MAIN CODE
C
 1010 ETAM = (1.0D0 - ETA)*HALF
      ETAP = (1.0D0 + ETA)*HALF
      XIM = (1.0D0 - XI)*HALF
      XIP = (1.0D0 + XI)*HALF
      ZETAM = (1.0D0 - ZETA)*HALF
      ZETAP = (1.0D0 + ZETA)*HALF
C
C     SET SHAPE FUNCTIONS
C
      FUN(1) = XIM*ETAM*ZETAM
      FUN(2) = XIM*ETAP*ZETAM
      FUN(3) = XIP*ETAP*ZETAM
      FUN(4) = XIP*ETAM*ZETAM
      FUN(5) = XIM*ETAM*ZETAP
      FUN(6) = XIM*ETAP*ZETAP
      FUN(7) = XIP*ETAP*ZETAP
      FUN(8) = XIP*ETAM*ZETAP
C
C     SET DERIVATVES OF SHAPE FUNCTIONS
C
      DER(1,1) = -ETAM*ZETAM*HALF
      DER(1,2) = -ETAP*ZETAM*HALF
      DER(1,3) = ETAP*ZETAM*HALF
      DER(1,4) = ETAM*ZETAM*HALF
      DER(1,5) = -ETAM*ZETAP*HALF
      DER(1,6) = -ETAP*ZETAP*HALF
      DER(1,7) = ETAP*ZETAP*HALF
      DER(1,8) = ETAM*ZETAP*HALF
      DER(2,1) = -XIM*ZETAM*HALF
      DER(2,2) = XIM*ZETAM*HALF
      DER(2,3) = XIP*ZETAM*HALF
      DER(2,4) = -XIP*ZETAM*HALF
      DER(2,5) = -XIM*ZETAP*HALF
      DER(2,6) = XIM*ZETAP*HALF
      DER(2,7) = XIP*ZETAP*HALF
      DER(2,8) = -XIP*ZETAP*HALF
      DER(3,1) = -XIM*ETAM*HALF
      DER(3,2) = -XIM*ETAP*HALF
      DER(3,3) = -XIP*ETAP*HALF
      DER(3,4) = -XIP*ETAM*HALF
      DER(3,5) = XIM*ETAM*HALF
      DER(3,6) = XIM*ETAP*HALF
      DER(3,7) = XIP*ETAP*HALF
      DER(3,8) = XIP*ETAM*HALF
C
      RETURN
      END
C***********************************************************************
