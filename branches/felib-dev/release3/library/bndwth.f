C***********************************************************************
      SUBROUTINE BNDWTH(ELTOP, IELTOP, JELTOP, NF, INF, JNF,
     *     DOFNOD, TOTELS, HBAND, ITEST)
C-----------------------------------------------------------------------
C PURPOSE
C      CALCULATES THE MAXIMUM FREEDOM NUMBER DIFFERENCE OVER
C      ALL THE ELEMENTS IN THE MESH
C
C HISTORY
C
C      COPYRIGHT (C) 1997 : CLRC, RUTHERFORD APPLETON LABORATORY
C                           CHILTON, DIDCOT, OXFORDSHIRE OX11 0QX
C
C      RELEASE 3.0  29 OCT 1984 (CG)
C
C ARGUMENTS IN
C      ELTOP   ELTOP(I,1) = ELEMENT TYPE OF ELEMENT I
C              ELTOP(I,2) = NUMBER OF NODES ON ELEMENT I
C              ELTOP(I,J+2), J=1(1)NUMBER OF NODES ON ELEMENT,
C              CONTAINS THE NODES ASSOCIATED WITH ELEMENT I
C      IELTOP  FIRST DIMENSION OF ARRAY ELTOP (.GE. TOTELS)
C      JELTOP  SECOND DIMENSION OF ELTOP (.GE. NUMBER OF NODES
C              ON ELEMENT)
C      NF      NF(I,J) CONTAINS THE FREEDOM NUMBERS ASSOCIATED
C              WITH NODE I
C      INF     FIRST DIMENSION OF NF (.GE. MAXIMUM NODE NUMBER
C              ON ELEMENT)
C      JNF     SECOND DIMENSION OF NF (.GE. DOFNOD)
C      DOFNOD  NUMBER OF DEGREES OF FREEDOM PER NODE ON THE
C              ELEMENT
C      TOTELS  NUMBER OF ELEMENTS IN THE MESH
C      ITEST   ERROR CHECKING OPTION
C
C ARGUMENTS OUT
C      HBAND   SEMI BANDWIDTH
C
C ROUTINES CALLED
C      ERRMES
C
C     SUBROUTINE BNDWTH(ELTOP, IELTOP, JELTOP, NF, INF, JNF,
C    *     DOFNOD, TOTELS, HBAND, ITEST)
C*********************************************************
C
      INTEGER DOFNOD, ELTOP, ERRMES, HBAND, I, IDEG, IELE,
     *     IELTOP, IERROR, INF, INOD, ITEST, J, JELTOP, JNF,
     *     JTEST, MAX, MAXINT, MIN, NF, NODEL, TOTELS
      DOUBLE PRECISION SRNAME
      DIMENSION ELTOP(IELTOP,JELTOP), NF(INF,JNF)
      DATA SRNAME /8H BNDWTH /
C
C     INITIALISATION
C
      HBAND = 0
      JTEST = ITEST
C
C     PARAMETER CHECKING
C
      IF (JTEST.EQ.-1) GO TO 1010
      IERROR = 0
      IF (DOFNOD.LE.0) IERROR = 1
      IF (JNF.LT.DOFNOD) IERROR = 2
      IF (TOTELS.GT.IELTOP) IERROR = 3
      ITEST = ERRMES(JTEST,IERROR,SRNAME)
      IF (ITEST.NE.0) RETURN
C
C     MAIN SCANNING LOOPS
C
 1010 DO 1060 IELE=1,TOTELS
         MAX = 0
         MIN = MAXINT(MAX)
         NODEL = ELTOP(IELE,2)
C
C     RANGE CHECKING ON NODEL
C
         IF (JTEST.EQ.-1) GO TO 1020
         IERROR = 0
         IF (JELTOP.LT.NODEL+2) IERROR = 4
         ITEST = ERRMES(JTEST,IERROR,SRNAME)
         IF (ITEST.NE.0) RETURN
C
C     LOOP OVER NODES
C
 1020    DO 1050 I=1,NODEL
            INOD = ELTOP(IELE,I+2)
C
C     RANGE CHECKING ON INOD
C
            IF (JTEST.EQ.-1) GO TO 1030
            IERROR = 0
            IF (INF.LT.INOD) IERROR = 5
            ITEST = ERRMES(JTEST,IERROR,SRNAME)
            IF (ITEST.NE.0) RETURN
C
C     LOOP OVER FREEDOMS
C
 1030       DO 1040 J=1,DOFNOD
               IDEG = NF(INOD,J)
               IF (IDEG.EQ.0) GO TO 1040
               MAX = MAX0(IDEG,MAX)
               MIN = MIN0(IDEG,MIN)
 1040       CONTINUE
 1050    CONTINUE
C
C     MAXIMUM FREEDOM NUMBER DIFFERENCE
C
         HBAND = MAX0(HBAND,MAX-MIN)
 1060 CONTINUE
C
C     SEMI BAND WIDTH
C
      HBAND = HBAND + 1
C
      RETURN
      END
C***********************************************************************
