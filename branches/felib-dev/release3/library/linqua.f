C***********************************************************************
      SUBROUTINE LINQUA(XI, ETA, GEOM, IGEOM, JGEOM, NODEL,
     *     SIDNUM, ALEN, ITEST)
C-----------------------------------------------------------------------
C PURPOSE
C      CALCULATES A UNIT OF LENGTH ALONG THE SIDE OF A
C      RECTANGULAR ELEMENT (4, 8 OR 12 NODED)
C
C HISTORY
C
C      COPYRIGHT (C) 1997 : CLRC, RUTHERFORD APPLETON LABORATORY
C                           CHILTON, DIDCOT, OXFORDSHIRE OX11 0QX
C
C      RELEASE 2.0   1  FEB 1981 (CG)
C      COMMENTED    24 JULY 1981 (CG)
C
C ARGUMENTS IN
C      XI      LOCAL COORDINATE OF POINT AT WHICH LENGTH IS
C              REQUIRED
C      ETA     LOCAL COORDINATE OF POINT AT WHICH LENGTH IS
C              REQUIRED
C      GEOM    LOCAL COORDINATE ARRAY CONTAINING THE GLOBAL
C              COORDIANTES OF EACH NODE ON AN ELEMENT IN THE
C              LOCAL ORDER
C      IGEOM   FIRST DIMENSION OF ARRAY GEOM (.GE.NODEL)
C      JGEOM   SECOND DIMENSION OF ARRAY GEOM (.GE.2)
C      NODEL   NUMBER OF NODES ON THE ELEMENT
C      SIDNUM  THE SIDE NUMBER OF THE SIDE OF THE ELEMENT TO
C              BE USED IN CALCULATING THE LENGTH (.LE.3)
C      ITEST   ERROR CHECKING OPTION
C
C ARGUMENTS OUT
C      ALEN    THE UNIT OF LENGTH AT THE SPECIFIED POINT
C
C ROUTINES CALLED
C      ERRMES  QUAM4  QUAM8  QUAM12  MATMUL
C
C
C     SUBROUTINE LINQUA(XI, ETA, GEOM, IGEOM, JGEOM, NODEL,
C    *     SIDNUM, ALEN, ITEST)
C***********************************************************************
C
      INTEGER DIMEN, ERRMES, IDER, IERROR, IFUN, IGEOM, IJAC,
     *     ITEST, JDER, JGEOM, JJAC, JTEST, N, NODEL, SIDNUM
      DOUBLE PRECISION ALEN, DER, ETA, FUN, GEOM, JAC, SRNAME,
     *     XI
      DIMENSION DER(2,12), FUN(12), GEOM(IGEOM,JGEOM),
     *     JAC(2,2)
      DATA DIMEN /2/, IDER /2/, IFUN /12/, IJAC /2/, JDER /12/,
     *     JJAC /2/, SRNAME /8H LINQUA /
C
C     PARAMETER CHECKING
C
      JTEST = ITEST
      IF (JTEST.EQ.-1) GO TO 1010
      IERROR = 0
      IF ((NODEL.LE.0) .OR. (SIDNUM.LE.0)) IERROR = 1
      IF (IGEOM.LT.NODEL .OR. JGEOM.LT.2) IERROR = 2
      IF (SIDNUM.LT.1 .OR. SIDNUM.GT.4) IERROR = 3
      ITEST = ERRMES(JTEST,IERROR,SRNAME)
      IF (ITEST.NE.0) RETURN
C
C     MAIN BODY
C
 1010 N = NODEL/4
C
C     RANGE CHECKING ON N (SHOULD 1, 2 OR 3)
C
      IF (JTEST.EQ.-1) GO TO 1020
      IERROR = 0
      IF (N.LE.0 .OR. N.GE.4) IERROR = 4
      ITEST = ERRMES(JTEST,IERROR,SRNAME)
      IF (ITEST.NE.0) RETURN
C
 1020 GO TO (1030, 1040, 1050), N
C
 1030 CALL QUAM4(FUN, IFUN, DER, IDER, JDER, XI, ETA, ITEST)
      GO TO 1060
 1040 CALL QUAM8(FUN, IFUN, DER, IDER, JDER, XI, ETA, ITEST)
      GO TO 1060
 1050 CALL QUAM12(FUN, IFUN, DER, IDER, JDER, XI, ETA, ITEST)
C
 1060 CALL MATMUL(DER, IDER, JDER, GEOM, IGEOM, JGEOM, JAC, IJAC,
     *     JJAC, DIMEN, NODEL, DIMEN, ITEST)
C
C     SELECT CORRECT SIDE OF ELEMENT
C
      GO TO (1070, 1080, 1070, 1080), SIDNUM
 1070 ALEN = DSQRT(JAC(2,1)**2+JAC(2,2)**2)
      RETURN
 1080 ALEN = DSQRT(JAC(1,1)**2+JAC(1,2)**2)
      RETURN
C
      END
C***********************************************************************
