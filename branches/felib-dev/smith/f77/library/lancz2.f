      SUBROUTINE LANCZ2(N,LALFA,LP,ITAPE,EIG,JEIG,NEIG,
     +                  ALFA,BETA,LY,LZ,JFLAG,Y,W,Z)
C FIND APPROXIMATE EIGENVECTORS CORRESPONDING TO EIGENVALUES FOUND
C     BY LANCZ1.
C N MUST BE SET TO THE MATRIX ORDER. IT IS NOT ALTERED.
C LALFA MUST BE SET TO THE LENGTH OF ARRAYS ALFA AND BETA. IT
C     IS NOT ALTERED.
C LP MUST BE SET TO THE UNIT NUMBER FOR DIAGNOSTIC MESSAGES. IF LP.LE.0
C     THE MESSAGES ARE SUPPRESSED. IT IS NOT ALTERED.
C ITAPE MUST BE SET TO THE UNIT NUMBER OF THE SEQUENTIAL FILE USED
C     BY LANCZ1. IT IS NOT ALTERED.
C EIG MUST CONTAIN EIGENVALUES AS FOUND BY LANCZ1. IT IS NOT ALTERED.
C JEIG MUST HOLD THE INFORMATION PASSED DOWN BY LANCZ1, NAMELY JEIG(1,I)
C     MUST CONTAIN  THE LANCZOS STEP AT WHICH EIG(I) WAS ACCEPTED AND
C     JEIG(2,I) MUST CONTAIN THE MATCHING POINT FOR THE EIGENVECTOR
C     CORRESPONDING TO EIG(I). JEIG IS NOT ALTERED.
C NEIG MUST HOLD THE NUMBER OF EIGENVECTORS WANTED. IT IS NOT ALTERED.
C ALFA,BETA MUST BE AS LEFT BY LANCZ1. THEY ARE NOT ALTERED.
C LY MUST HOLD THE FIRST DIMENSION OF Y. IT MUST BE AT LEAST N. IT IS
C     NOT ALTERED.
C LZ MUST HOLD THE FIRST DIMENSION OF Z. IT MUST BE AT LEAST
C     MAX(JEIG(1,I),I=1,NEIG). IT IS NOT ALTERED.
C IFLAG NEED NOT BE SET. ON RETURN IT HAS ONE OF THE VALUES
C      0    SUCCESSFUL COMPLETION
C      1    N.LE.0 .OR. LALFA.LE.0 .OR. ITAPE.LE.0 .OR. NEIG.LE.0
C      2    LY TOO SMALL
C      3    LZ TOO SMALL
C Y IS SET TO THE REQUIRED EIGENVECTORS.
C W IS USED AS A WORKVECTOR.
C Z IS USED AS A WORKVECTOR.
C LZ MUST BE SET TO THE FIRST DIMENSION OF Z. IT MUST BE AT LEAST
C     MAX(JEIG(1,I),I=1,2,...,NEIG).
      REAL             EIG(NEIG),ALFA(LALFA),BETA(LALFA),
     +                 Y(LY,NEIG),W(N),Z(LZ,NEIG)
      INTEGER JEIG(2,NEIG)
      REAL             S
      REAL             ZERO,ONE
      DATA ZERO/0.0E0/, ONE/1.0E0/
      JFLAG=0
      IF(N.LE.0 .OR. LALFA.LE.0 .OR. ITAPE.LE.0 .OR.NEIG.LE.0)GO TO 100
      IF(LY.LT.N)GO TO 120
      M=0
      DO 20 L=1,NEIG
      M=MAX0(M,JEIG(1,L))
      IF(LZ.LT.M)GO TO 140
      DO 10 I=1,N
      Y(I,L)=ZERO
   10 CONTINUE
      CALL EA15GD(JEIG(1,L),LALFA,ALFA,BETA,EIG(L),JEIG(2,L),Z(1,L))
   20 CONTINUE
      DO 50 J=1,M
      CALL EA15FD(N,ITAPE,J,2,W)
      DO 40 L=1,NEIG
      IF(J.GT.JEIG(1,L))GO TO 40
      DO 30 I=1,N
      Y(I,L)=Y(I,L)+Z(J,L)*W(I)
   30 CONTINUE
   40 CONTINUE
   50 CONTINUE
C
C NORMALIZE THE VECTORS
      DO 90 L=1,NEIG
      S=ZERO
      DO 70 I=1,N
      S=S+Y(I,L)**2
   70 CONTINUE
      S=ONE/ SQRT(S)
      DO 80 I=1,N
      Y(I,L)=Y(I,L)*S
   80 CONTINUE
   90 CONTINUE
      GO TO 150
  100 JFLAG=1
      IF(LP.GT.0)WRITE(LP,110)N,LALFA,ITAPE,NEIG
  110 FORMAT(28H ERROR RETURN 1 FROM LANCZ2./
     +      3H N=,I6,7H LALFA=,I6,7H ITAPE=,I6,6H NEIG=,I6)
      GO TO 150
  120 JFLAG=2
      IF(LP.GT.0)WRITE(LP,130)LY,N
  130 FORMAT(32H ERROR RETURN 2 FROM LANCZ2. LY=,I6/
     +       23H AND SHOULD BE AT LEAST,I6)
      GO TO 150
  140 JFLAG=3
      IF(LP.GT.0)WRITE(LP,145)LZ,M
  145 FORMAT(32H ERROR RETURN 3 FROM LANCZ2. LZ=,I6/
     +       23H AND SHOULD BE AT LEAST,I6)
  150 RETURN
      END
