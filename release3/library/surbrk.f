C***********************************************************************
      SUBROUTINE SURBRK(XI, ETA, ZETA, GEOM, IGEOM, JGEOM, NODEL,
     *     FACNUM, UAREA, ITEST)
C-----------------------------------------------------------------------
C PURPOSE
C      CALCULATES A UNIT OF AREA ON THE FACE OF A
C      BRICK ELEMENT (8, 20 OR 32 NODED)
C
C HISTORY
C
C      COPYRIGHT (C) 1997 : CLRC, RUTHERFORD APPLETON LABORATORY
C                           CHILTON, DIDCOT, OXFORDSHIRE OX11 0QX
C
C      RELEASE 2.0  1  FEB 1981 (CG)
C      COMMENTED    24 JUL 1981 (CG)
C
C ARGUMENTS IN
C      XI      LOCAL COORDINATE OF POINT AT WHICH AREA IS
C              REQUIRED
C      ETA     LOCAL COORDINATE OF POINT AT WHICH AREA IS
C              REQUIRED
C      ZETA    LOCAL COORDINATE OF POINT AT WHICH AREA IS
C              REQUIRED
C      GEOM    LOCAL COORDINATE ARRAY CONTAINING THE GLOBAL
C              COORDIANTES OF EACH NODE ON AN ELEMENT IN THE
C              LOCAL ORDER
C      IGEOM   FIRST DIMENSION OF ARRAY GEOM (.GE.NODEL)
C      JGEOM   SECOND DIMENSION OF ARRAY GEOM (.GE.3)
C      NODEL   NUMBER OF NODES ON THE ELEMENT
C      FACNUM  THE FACE NUMBER OF THE FACE OF THE ELEMENT TO
C              BE USED IN CALCULATING THE AREA (.LE.6)
C      ITEST   ERROR CHECKING OPTION
C
C ARGUMENTS OUT
C      UAREA    THE UNIT OF AREA AT THE SPECIFIED POINT
C
C ROUTINES CALLED
C      ERRMES  BRK8  BRK20  BRK32  MATMUL
C
C
C     SUBROUTINE SURBRK(XI, ETA, ZETA, GEOM, IGEOM, JGEOM, NODEL,
C    *     FACNUM, UAREA, ITEST)
C***********************************************************************
C
      INTEGER DIMEN, ERRMES, FACNUM, I, IDER, IERROR, IFUN,
     *     IGEOM, IJAC, ITEST, J, JDER, JGEOM, JJAC, JTEST,
     *     N, NODEL
      DOUBLE PRECISION DER, ETA, FUN, G1, G2, G3, GEOM, JAC,
     *     SRNAME, UAREA, XI, ZETA
      DIMENSION DER(3,32), FUN(32), GEOM(IGEOM,JGEOM),
     *     JAC(3,3)
      DATA DIMEN /3/, IDER /3/, IFUN /32/, IJAC /3/, JDER /32/,
     *     JJAC /3/, SRNAME /8H SURBRK /
C
C     PARAMETER CHECKING
C
      JTEST = ITEST
      IF (JTEST.EQ.-1) GO TO 1010
      IERROR = 0
      IF ((NODEL.LE.0) .OR. (FACNUM.LE.0)) IERROR = 1
      IF (IGEOM.LT.NODEL .OR. JGEOM.LT.3) IERROR = 2
      IF (FACNUM.LT.1 .OR. FACNUM.GT.6) IERROR = 3
      ITEST = ERRMES(JTEST,IERROR,SRNAME)
      IF (ITEST.NE.0) RETURN
C
C     MAIN BODY
C
 1010 N = NODEL/8
C
C     RANGE CHECKING ON N
C
      IF (JTEST.EQ.-1) GO TO 1020
      IERROR = 0
      IF (N.LE.0 .OR. N.GE.4) IERROR = 4
      ITEST = ERRMES(JTEST,IERROR,SRNAME)
      IF (ITEST.NE.0) RETURN
C
C     GET SHAPE FUNCTION DERIVATIVES
C
 1020 GO TO (1030, 1040, 1050), N
      IF(JTEST.EQ.-1) GOTO 1030
      IERROR = 4
      ITEST = ERRMES(JTEST,IERROR,SRNAME)
      RETURN
C
 1030 CALL BRK8(FUN, IFUN, DER, IDER, JDER, XI, ETA, ZETA, ITEST)
      GO TO 1060
 1040 CALL BRK20(FUN, IFUN, DER, IDER, JDER, XI, ETA, ZETA, ITEST)
      GO TO 1060
 1050 CALL BRK32(FUN, IFUN, DER, IDER, JDER, XI, ETA, ZETA, ITEST)
C
C     CALCULATE GLOBAL DERIVATIVES
C
 1060 CALL MATMUL(DER, IDER, JDER, GEOM, IGEOM, JGEOM, JAC, IJAC,
     *     JJAC, DIMEN, NODEL, DIMEN, ITEST)
C
      GO TO (1090, 1090, 1070, 1080, 1070, 1080), FACNUM
C
C    XI = CONSTANT
C
 1070 I = 2
      J = 3
      GO TO 1100
C
C    ETA = CONSTANT
C
 1080 I = 1
      J = 3
      GO TO 1100
C
C    ZETA = CONSTANT
C
 1090 I = 1
      J = 2
C
 1100 G1 = JAC(I,2)*JAC(J,3) - JAC(I,3)*JAC(J,2)
      G2 = JAC(I,3)*JAC(J,1) - JAC(I,1)*JAC(J,3)
      G3 = JAC(I,1)*JAC(J,2) - JAC(I,2)*JAC(J,1)
      UAREA = DSQRT(G1**2+G2**2+G3**2)
C
      RETURN
      END
C***********************************************************************
