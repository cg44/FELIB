C***********************************************************************
      SUBROUTINE CPRTVL(VAL, IVAL, NF, INF, JNF, DOFNOD, TOTNOD,
     *     NOUT, ITEST)
C-----------------------------------------------------------------------
C PURPOSE
C      PRINTS OUT THE NODAL VALUES OF THE SOLUTION IN A
C      STANDARD FORMAT
C
C HISTORY
C
C      COPYRIGHT (C) 1997 : CLRC, RUTHERFORD APPLETON LABORATORY
C                           CHILTON, DIDCOT, OXFORDSHIRE OX11 0QX
C
C      RELEASE 3.0  20 JAN 1984 (CRIE)
C      COMMENTS      1 NOV 1985 (CG)
C
C ARGUMENTS IN
C      VAL     COMPLEX VECTOR OF DIMENSION 2*IVAL CONTAINING
C              SOLUTION VALUES,AND PRESCRIBED BOUNDARY VALUES
C      IVAL    DIMENSION OF VAL (.GE.TOTAL NUMBER OF FREEDOMS
C              IN SYSTEM)
C      NF      INTEGER ARRAY OF DIMENSION (INF,JNF) CONTAINING
C              FREEDOM NUMBERS ASSOCIATED WITH EACH NODE
C      INF     FIRST DIMENSION OF NF (.GE.TOTNOD)
C      JNF     SECOND DIMENSION OF NF (.GE.DOFNOD)
C      DOFNOD  NUMBER OF DEGREES OF FREEDOM AT EACH NODE
C      TOTNOD  TOTAL NUMBER OF NODES IN MESH
C      NOUT    FORTRAN UNIT NUMBER
C      ITEST   ERROR CHECKING OPTION
C
C ROUTINES CALLED
C      ERRMES
C
C     SUBROUTINE CPRTVL(VAL, IVAL, NF, INF, JNF, DOFNOD,
C    *      TOTNOD, NOUT, ITEST)
C***********************************************************************
C
      INTEGER DOFNOD, ERRMES, I, IERROR, INF ,ITEST, IVAL,
     *     J, JNF, K, L, NODE, NOUT, TOTNOD, N, NF
      DOUBLE PRECISION VAL, WORK, SRNAME
      DIMENSION VAL(2,IVAL), WORK(2,10), NF(INF,JNF), NODE(2)
      DATA SRNAME /8H CPRTVL /
C
C     PARAMETER CHECKING
C
                        IF (ITEST.EQ.-1) GO TO 1000
                        IERROR = 0
                        IF (INF.LT.TOTNOD .OR. JNF.LT.DOFNOD)
     *                      IERROR = 2
                        IF (DOFNOD.LE.0 .OR. TOTNOD.LE.0 .OR. NOUT.LE.0)
     *                      IERROR = 1
                        IF (DOFNOD.GT.10)
     *                      IERROR = 1
                        ITEST = ERRMES(IERROR,ITEST,SRNAME)
                        IF (ITEST.NE.0) RETURN
C
C     MAIN LOOPS
C
 1000 CONTINUE
      IF (DOFNOD.EQ.1) GOTO 1110
      WRITE(NOUT,9010)
      DO 1100 I=1,TOTNOD
      DO 1020 J=1,DOFNOD
      K=NF(I,J)
      WORK(1,J)=0.0D0
      WORK(2,J)=0.0D0
      IF (K.EQ.0) GOTO 1010
      WORK(1,J) = VAL(1,K)
      WORK(2,J) = VAL(2,K)
 1010 CONTINUE
 1020 CONTINUE
C
C     SELECT NUMBER OF DEGREES OF FREEDOM
C
      GOTO (1090,1040,1030,1050,1030,1060,1030,1070,1030,1080),DOFNOD
 1030 CONTINUE
      WRITE(NOUT,9020)I,(WORK(1,L),WORK(2,L),L=1,DOFNOD)
      GOTO 1090
 1040 CONTINUE
      WRITE(NOUT,9030)I,(WORK(1,L),WORK(2,L),L=1,DOFNOD)
      GOTO 1090
 1050 CONTINUE
      WRITE(NOUT,9040)I,(WORK(1,L),WORK(2,L),L=1,DOFNOD)
      GOTO 1090
 1060 CONTINUE
      WRITE(NOUT,9050)I,(WORK(1,L),WORK(2,L),L=1,DOFNOD)
      GOTO 1090
 1070 CONTINUE
      WRITE(NOUT,9060)I,(WORK(1,L),WORK(2,L),L=1,DOFNOD)
      GOTO 1090
 1080 CONTINUE
      WRITE(NOUT,9070)I,(WORK(1,L),WORK(2,L),L=1,DOFNOD)
 1090 CONTINUE
 1100 CONTINUE
      RETURN
 1110 CONTINUE
C
C     FOR DOFNOD=1
C
      WRITE(NOUT,9080)
      N=0
      DO 1140 I=1,TOTNOD
      N=N+1
      NODE(N)=I
      K=NF(I,1)
      WORK(1,N)=0.0D0
      WORK(2,N)=0.0D0
      IF(K.EQ.0) GOTO 1120
      WORK(1,N)=VAL(1,K)
      WORK(2,N)=VAL(2,K)
 1120 CONTINUE
      IF(N.NE.2) GOTO 1140
      WRITE(NOUT,9090)(NODE(J),WORK(1,J),WORK(2,J),J=1,2)
      N=0
 1140 CONTINUE
      IF(N.EQ.0) RETURN
      WRITE(NOUT,9090) NODE(1),WORK(1,1),WORK(2,1)
      RETURN
C
 9010 FORMAT(/1H ,4HNODE,6X,2(4HREAL,7X,9HIMAGINARY,16X)/1H )
 9020 FORMAT(1H ,I4,2X,2(D12.5,2X),8X,2(D12.5,2X),8(/1H ,6X,2(D12.5,2X),
     *     8X,2(D12.5,2X) ))
 9030 FORMAT(1H ,I4,2X,2(D12.5,2X),8X,2(D12.5,2X) )
 9040 FORMAT(1H ,I4,2X,2(D12.5,2X),8X,2(D12.5,2X),1(/1H ,6X,2(D12.5,2X),
     *     8X,2(D12.5,2X) ))
 9050 FORMAT(1H ,I4,2X,2(D12.5,2X),8X,2(D12.5,2X),2(/1H ,6X,2(D12.5,2X),
     *     8X,2(D12.5,2X) ))
 9060 FORMAT(1H ,I4,2X,2(D12.5,2X),8X,2(D12.5,2X),3(/1H ,6X,2(D12.5,2X),
     *     8X,2(D12.5,2X) ))
 9070 FORMAT(1H ,I4,2X,2(D12.5,2X),8X,2(D12.5,2X),4(/1H ,6X,2(D12.5,2X),
     *     8X,2(D12.5,2X) ))
 9080 FORMAT(/1H ,2(4HNODE,6X,4HREAL,7X,9HIMAGINARY,6X)/1H )
 9090 FORMAT(2(1H ,I4,2X,2(D12.5,2X),1X))
      END
C***********************************************************************
