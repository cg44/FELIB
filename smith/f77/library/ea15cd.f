      SUBROUTINE EA15CD(J,LALFA,ALFA,BETA,E,ENORM,ERR,BND,MATCH)
C GIVEN A TRIDIAGONAL MATRIX T GENERATED BY THE LANCZOS PROCESS APPLIED
C     TO A GIVEN SYMMETRIC MATRIX A AND AN APPROXIMATE EIGENVALUE OF T,
C     FIND A BOUND FOR ITS ERROR AS AN EIGENVALUE OF A.
C J IS THE INDEX OF THE LANCZOS STEP. IT IS NOT ALTERED.
C LALFA MUST BE SET TO THE LENGTHS OF ARRAYS ALFA AND BETA. IT IS NOT
C     ALTERED.
C ALFA(I),I=1,J MUST BE SET TO THE DIAGONAL ELEMENTS OF THE LANCZOS
C     TRIDIAGONAL MATRIX T. THEY ARE NOT ALTERED.
C BETA(I),I=2,J+1 MUST BE SET TO THE OFF-DIAGONAL ELEMENTS OF THE
C     LANCZOS TRIDIAGONAL MATRIX T. THEY ARE NOT ALTERED.  NOTE THAT A
C     TYPICAL ROW OF T IS
C          BETA(I)  ALFA(I)  BETA(I+1)
C E MUST BE SET TO THE APPROXIMATE EIGENVALUE OF T. IT IS NOT ALTERED.
C ENORM MUST BE SET TO THE RELATIVE PRECISION TIMES THE NORM OF A TIMES
C     THE ORDER OF A. IT IS NOT ALTERED.
C ERR IS SET TO A BOUND FOR THE ERROR OF E.
C BND IS SET TO AN ESTIMATE OF THE AMOUNT THAT ERR MIGHT BE REDUCED BY
C     USING A MORE ACCURATE E AND/OR A MORE ACCURATE EIGENVECTOR
C     CALCULATION.
C MATCH IS SET TO THE MATCHING POINT BETWEEN USE OF FORWARD AND BACKWARD
C     RECURRENCES.
      REAL             ALFA(LALFA),BETA(LALFA),E,S,PSI,BND,ERR,ENORM
      REAL             W,W0,W1,W2,SW,Z1,Z2,BNDK,ERRK,WMAX10
      REAL             ZERO,ONE
      DATA ZERO/0.0E0/, ONE/1.0E0/
      J1=J-1
C
C START THE FORWARD RECURRENCE FOR SOLVING (T-E)*Z=PSI*EK
      W1=ZERO
      W2=ONE
      SW=ZERO
      K=0
      W=ALFA(1)-E
      WMAX10=ZERO
C
      DO 100 KTRY=1,10
      IF(K-J1)5,15,110
    5 K1=K+1
C CONTINUE THE FORWARD RECURRENCE UNTIL THE FIRST MAXIMUM
C     OR A MAXIMUM AT LEAST TEN TIMES BIGGER THAN THE PREVIOUS
C     MAXIMUM IS FOUND.
      DO 10 K=K1,J1
      W0=W1
      W1=W2
      W2=-W/BETA(K+1)
      SW=SW+W1**2
      W=(ALFA(K+1)-E)*W2+BETA(K+1)*W1
      IF( ABS(W1).LT.WMAX10)GO TO 10
      IF( ABS(W1).GE.AMAX1( ABS(W0), ABS(W2)))GO TO 20
   10 CONTINUE
   15 K=J
      W0=W1
      W1=W2
      SW=SW+W1**2
      IF( ABS(W1).LT.WMAX10)GO TO 100
      IF( ABS(W1).LT. ABS(W0))GO TO 100
C
C FORWARD RECURRENCE SHOWS A MAXIMUM AT COMPONENT K. CONTINUE
C     SOLVING (T-E)*Z=PSI*EK BY BACKWARD RECURRENCE
   20 S=ZERO
      WMAX10= ABS(W1)*10.
      Z1=ONE
      PSI=ALFA(J)-E
      IF(K.GT.J1)GO TO 40
      DO 30 KK=K,J1
      I=K+J1-KK
      Z2=Z1
      Z1=-PSI/BETA(I+1)
      S=S+Z2**2
      PSI=(ALFA(I)-E)*Z1+BETA(I+1)*Z2
   30 CONTINUE
   40 IF(W1.EQ.ZERO)GO TO 100
C
C FORM PRESENT ERROR BOUND AND STORE THE BEST SO FAR FOUND
      Z1=Z1/W1
      S=S+SW*Z1**2
      S= SQRT(S)
      IF(K.GT.1)PSI=PSI+BETA(K)*Z1*W0
      BNDK=1.25* ABS(PSI)/S
      ERRK=1.25*(ENORM+BETA(J+1)/S)+BNDK
      IF(KTRY.EQ.1)GO TO 50
      IF(ERR.LT.ERRK)GO TO 90
   50 ERR=ERRK
      BND=BNDK
      MATCH=K
C
C TEST RESULT FOR ACCEPTABILITY
   90 IF(BNDK.LT.ERRK/5.)GO TO 110
  100 CONTINUE
  110 RETURN
      END
